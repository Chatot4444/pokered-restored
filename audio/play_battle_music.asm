PlayBattleMusic::
	xor a
	ld [wMusicFade], a
	ld [wLowHealthAlarm], a
	dec a ; SFX_STOP_ALL_MUSIC
;	ld [wNewSoundID], a
	call PlaySound
	call DelayFrame
	ld c, 0 ; BANK(Music_GymLeaderBattle)
	ld a, [wGymLeaderNo]
	and a
	jr z, .notGymLeaderBattle
	ld a, MUSIC_GYM_LEADER_BATTLE
	jr .playSong
.notGymLeaderBattle
	ld a, [wIsTrainerBattle]
	and a
	jr z, .wildBattle
	ld a, [wCurOpponent]
	cp OPP_RIVAL3
	jr z, .finalBattle
	cp OPP_LORELEI
	jr c, .normalTrainerBattle
	ld a, MUSIC_GYM_LEADER_BATTLE ; lance also plays gym leader theme
	jr .playSong
.normalTrainerBattle
	ld a, MUSIC_TRAINER_BATTLE
	jr .playSong
.finalBattle
	ld a, MUSIC_FINAL_BATTLE
	jr .playSong
.wildBattle
	ld a, [wCurOpponent]
	cp ARTICUNO
	jr z, .bird
	cp ZAPDOS
	jr z, .bird
	cp MOLTRES
	jr z, .bird
	cp ARTICUNOG
	ld a, MUSIC_WILD_BATTLE
	jr c, .playSong
	ld a, MUSIC_GALAR_BIRD_BATTLE
.playSong
	jp PlayMusic

.bird ;bird
	ld a, MUSIC_KANTO_BIRD_BATTLE
	jr .playSong